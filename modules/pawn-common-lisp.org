#+TITLE:Common Lisp
#+OPTIONS: toc:2 num:nil ^:nil
| precondition | repository |
|--------------+------------|
| sbcl         | pacman     |

| packages |
|----------|
| slime    |

#+BEGIN_SRC emacs-lisp
(use-package slime
  :ensure t
  :defer t
  :init
  (setq inferior-lisp-program "/usr/bin/sbcl --noinform")
  :load-path "/usr/share/emacs/site-lisp/slime/"
  :config
  (require 'slime-repl)

  (defvar pawn/slime-repl-original-buffer nil
    "Buffer from which we jumped to this REPL.")

  (defvar pawn/slime-repl-switch-function 'switch-to-buffer-other-window)

  (defun pawn/slime-switch-to-output-buffer ()
    (interactive)
    (setq pawn/slime-repl-original-buffer (current-buffer))
    (if (slime-connected-p)
        (slime-switch-to-output-buffer)
      (slime)))

  (defun pawn/slime-repl-switch-back ()
    "Switch back to the buffer from witch we reached this REPL."
    (interactive)
    (if pawn/slime-repl-original-buffer
        (funcall pawn/slime-repl-switch-function pawn/slime-repl-original-buffer)
      (error "No original buffer.")))

  (defun pawn/slime-eval-and-replace ()
    (interactive)
    (slime-eval-async `(swank:eval-and-grab-output ,(slime-last-expression))
      (lambda (result)
        (cl-destructuring-bind (output value) result
          (backward-kill-sexp)
          (insert output value)))))

 (bind-key "C-c C-z" 'pawn/slime-switch-to-output-buffer slime-mode-map)
 (bind-key "C-c C-z" 'pawn/slime-repl-switch-back slime-repl-mode-map)
 (bind-key "C-x M-e" 'pawn/slime-eval-and-replace slime-mode-map)
 (add-hook 'slime-repl-mode-hook 'lisp-setup)
 (slime-setup '(slime-fancy)))
#+END_SRC

slime에서 ,을 누르는 것을 통해 command을 입력하는 것이 가능하다.

자세한 레페런스를 보기 위해서는 Common Lisp HyperSpec가 좋다.
~C-c C-d h~ 를 누르면 HyperSpec 페이지로 이동한다.
